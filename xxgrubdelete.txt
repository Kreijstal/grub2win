@echo off
%systemdrive%
set diag=

:mainroutine
        if "%diag%" neq "" echo ** mainroutine **
        call :clearit
	%systemdrive%
	ver | find "5.1" 1>nul
        if "%errorlevel%" == "0" set xpsystem=y
        if exist %basepath%  (
		set message1=         Deleting the Grub2Win product directory  %basepath%
		call :displayit 2
		call :removedir %basepath%
		call :sleeper 1
	)
	call :delolddir
	call :finalcloseout
exit

:finalcloseout
        if "%diag%" neq "" echo ** finalcloseout **
        call :clearit
        call :cleanwork
        if not exist %basepath% (
		set message1=                         ** Success **
		set message3=         Grub2Win has been uninstalled from your system
		call :displayit 3 
        )
	set quiet=
        if defined LicenseWarnPID call :closewarn
	goto 2>nul & del "%~f0"
goto :eof
        
:delolddir
        if "%diag%" neq "" echo ** delolddir **
        call :clearit
	if not exist %basepath%.old* goto :eof
        call :clearit
	set message1=         Do you want to delete the Grub2Win backup directory?
	set message3=                         %basepath%.old    [n/y]?
	call :displayit 
	call :chooseit 
	if "%choosereturn%" EQU "y" ( set message1=         Deleting the Grub2Win backup directory
				      set message3=                   %basepath%.old.*
	                              call :displayit 1
	                              call :delmultidir
         )
	if "%choosereturn%" EQU "n" ( set message2=         The Grub2Win backup directory was not deleted
	                              call :displayit 2 
        )
goto :eof

:cleanwork
        if "%diag%" neq "" echo ** cleanwork **
        if "%xpsystem%" equ "y" (
       	   tskill grub2win.exe >nul 2>nul
       	   rd /s /q "C:\Documents and Settings\All Users\Application Data\Grub2Win" 2>nul
	) else                  (
           taskkill /f /t /im grub2win.exe >nul 2>nul
           rd /s /q %ProgramData%\Grub2Win 2>nul
        )
goto :eof

:closewarn
        if "%diag%" neq "" echo ** closewarn **
        call :sleeper 5
	if "%xpsystem%" neq "y" taskkill /f /t /pid %LicenseWarnPID% >nul 2>nul
        if "%xpsystem%" equ "y" tskill              %LicenseWarnPID% >nul 2>nul
        call :sleeper 1
        rd /s /q %basepath% 2>nul
	call :sleeper 1
goto :eof

:removedir
        if "%diag%" neq "" echo ** removedir ** %1 
        call :clearit 
	call :deleteit %1
        if defined quiet  goto  :eof
	if not exist %1 (
	     set message2=         Directory %1 was successfully deleted
	     call :displayit 2
             goto :eof
	)
        call :retryit %1
goto :eof

:deleteit
        if "%diag%" neq "" echo ** deleteit ** %1
	set delstatus=bad
	rd /s /q %1 2>nul
	call :sleeper 1
	if not exist %1 set delstatus=good
goto :eof
 
:retryit
        if "%diag%" neq "" echo ** retryit ** %1
        rem if "%xpsystem%" equ "y" goto :eof
        call :clearit
        set retrycount= 1 2 3
        for %%a in (%retrycount%) Do (
                if not exist %1 goto :eof
		set message1=       The delete failed for directory %1
		set message2=       A file in the directory may be in use
		set message3=       Do you want to retry the delete [n/y]?
                if "%xpsystem%" equ "y" (
                  set message1=       The delete was cancelled
		  set message3=       You should remove %1 manually
  		  call :displayit 
  		  pause
  		  goto :eof
		)
                call :displayit
		choice /n /c yn
	        if ERRORLEVEL 2 ( 
 		  set message1=       The delete was cancelled
  	 	  set message3=       You should remove %1 manually
  		  call :displayit 
  		  pause
  		  goto :eof
		)
		set message1=         Retrying the delete of %1
		set message3=         This is retry number %%a
		call :displayit 1
		call :deleteit  %1
	)
        set message1=         %1 was not deleted
	set message3=               After 3 retrys
	call :displayit 3
goto :eof

:delmultidir
        call :clearit
        if "%diag%" neq "" echo ** delmultidir **
        set "deldrive=%basepath:~0,3%"
        for /f %%i in ('dir /a:d /b C:\grub2.old*') do (
                set message1=         Deleting %deldrive%%%i
                call :displayit 1
		call :removedir %deldrive%%%i
                )
goto :eof

:chooseit
        if "%diag%" neq "" echo ** chooseit **
        set choosereturn=
       	if "%xpsystem%" equ "y" (call :deciderxp choosereturn 
	                         call :clearit
		                 goto :eof
	)
	if defined quiet        (set choosereturn=y
 	                        goto :eof
	)
	choice /n /c yn
	if ERRORLEVEL 1 set choosereturn=y
        if ERRORLEVEL 2 set choosereturn=n
goto :eof

:deciderxp
        if "%diag%" neq "" echo ** deciderxp **
        call :clearit
	if defined quiet        (set   "%~1=y"
	                         goto :eof
	)
	set /p decision=""
	if /i NOT "%decision%" equ "n" (
	    if /i NOT "%decision%" equ "y" (
		call :displayit 
 	   	goto deciderxp)
	)
	set   "%~1=%decision%"
goto :eof

:displayit
        if "%diag%" neq "" echo ** displayit ** %1
	if defined quiet goto :eof
	cls
	echo.
	echo.
	echo.
	if   "%message1%" neq "" echo %message1%
	echo.
	if   "%message2%" neq "" echo %message2%
	echo.
	if   "%message3%" neq "" echo %message3%
	echo.
	echo.
	echo.
        call :clearit
	if not "%1" equ "" call :sleeper %1
goto :eof

:clearit
        if "%diag%" neq "" echo ** clearit **
	set message1=
	set message2=
	set message3=
goto :eof

:sleeper
        if "%diag%" neq "" echo ** sleeper ** %1
	set /a seconds = %1 + 1
	ping -n %seconds% 127.0.0.1 >nul 2>&1 
goto :eof